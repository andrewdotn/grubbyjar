plugins {
    id 'java-gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile("junit:junit:4.12")
    testCompile("org.hamcrest:hamcrest-library:1.3")
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'ca.neitsch.grubyjar'
            implementationClass = 'ca.neitsch.grubyjar.GrubyJarPlugin'
        }
    }
}

tasks.withType(AbstractCompile) {
    options.compilerArgs << "-Werror" << "-Xlint" << "-Xlint:-serial"
}

task unitTest(type: Test) {
    exclude '**/*IntegTest.class'
}

task integTest(type: Test) {
    include '**/*IntegTest.class'
    shouldRunAfter unitTest
}

// Declared as type Test with no tests, because that makes IntelliJ listen for
// test results.
task unitThenIntegrationTests(type: Test) {
    exclude '*'
    dependsOn unitTest, integTest
}
